plugins {
    id("java-library")
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

// See "deploy" task registered below as to find out why it's here.
final buildsDirectory = (System.getenv("JVM_BUILDS") ?: null)
final pluginsDirectory = (System.getenv("PLUGINS_DIR") ?: null)

group = "cloud.grabsky"
version = "0.9.0-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://repo.grabsky.cloud/snapshots" }
    maven { url = "https://papermc.io/repo/repository/maven-public/" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.26")
    annotationProcessor("org.projectlombok:lombok:1.18.26")
    // Paper API
    compileOnly("io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT")
    // Utility, Commands and Configuration libraries
    implementation("cloud.grabsky:bedrock:52ad945")
    implementation("cloud.grabsky:commands:a2eb7f9")
    implementation("cloud.grabsky:configuration-paper:0.9.7")
    // PlaceholderAPI
    compileOnly("me.clip:placeholderapi:2.11.3")
}

tasks {
    shadowJar {
        archiveFileName = "${rootProject.name}-${project.version}.jar"
    }
    compileJava {
        options.fork = true
        // Setting compatibility to Java 17 (above should work too)
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

// Copies produced .jar to $JVM_BUILDS and $PLUGINS_DIR directories specfied as environment variables.
// This is solely for debugging/development purposes and does nothing when said variables are not set.
tasks.register("deploy") {
    dependsOn(shadowJar)
    doLast {
        // Copying to the builds directory specified by 'JVM_BUILDS' environment variable.
        if (buildsDirectory != null)
            copy { from(shadowJar) into(buildsDirectory) }
        // Copying to the plugins directory specified by 'PLUGINS_DIR' environment variable.
        if (pluginsDirectory != null)
            copy { from(shadowJar) into(pluginsDirectory) }
    }
}