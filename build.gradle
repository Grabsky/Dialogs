plugins {
    id("java-library")
    id 'net.minecrell.plugin-yml.paper' version '0.6.0'
}

// See "deploy" task registered below as to find out why it's here.
final buildsDirectory = (System.getenv("JVM_BUILDS") ?: null)
final pluginsDirectory = (System.getenv("PLUGINS_DIR") ?: null)

group = "cloud.grabsky"
version = "1.20.1-${System.getenv("GITHUB_RUN_NUMBER") ?: "dev"}"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://repo.grabsky.cloud/releases/" }
    maven { url = "https://papermc.io/repo/repository/maven-public/" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.26")
    annotationProcessor("org.projectlombok:lombok:1.18.26")
    // Paper API
    compileOnly("io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT")
    // Libraries
    paperLibrary("cloud.grabsky:bedrock:1.20.1-44")
    paperLibrary("cloud.grabsky:commands:1.20.1-34")
    paperLibrary("cloud.grabsky:configuration-paper:1.20.1-42")
    // PlaceholderAPI
    compileOnly("me.clip:placeholderapi:2.11.3")
}

tasks {
    jar {
        archiveFileName = "${rootProject.name}-${project.version}.jar"
    }
    compileJava {
        options.fork = true
        // Setting compatibility to Java 17 (above should work too).
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

// Copies produced .jar to $JVM_BUILDS and $PLUGINS_DIR directories specfied as environment variables.
// This is solely for debugging/development purposes and does nothing when said variables are not set.
tasks.register("deploy") {
    dependsOn(jar)
    doLast {
        // Copying to the builds directory specified by 'JVM_BUILDS' environment variable.
        if (buildsDirectory != null)
            copy { from(jar) into(buildsDirectory) }
        // Copying to the plugins directory specified by 'PLUGINS_DIR' environment variable.
        if (pluginsDirectory != null)
            copy { from(jar) into(pluginsDirectory) }
    }
}

paper {
    main = "cloud.grabsky.dialogs.Dialogs"
    loader = "cloud.grabsky.dialogs.Dialogs\$PluginLoader"
    apiVersion = "1.20"
    generateLibrariesJson = true
    serverDependencies {
        "PlaceholderAPI" { load = "BEFORE"; required = false }
    }
}